local kpmstd = import "kpm-utils.libjsonnet";
kpmstd {

   local kpm = self,
   utils:: kpmstd,

   variables(package, params):: (
   local p = package + {variables: package.variables + std.mergePatch(super.variables, params.variables)};
     p.variables
   ),

   deploy(dep):: (
      dep
   ),

   shards(s, target):: (
      if std.type(target) == 'null' then
         s
      else if std.type(target) == "object" then
         std.mergePatch(s, target)
      else if std.type(target) == "array" then
         if std.length(target) == 0 then
            s
         else
            target
      else if std.type(target) == "number" then
         [{ name: x } for x in kpm.seq(target)]
      else
         error "Bad shard type: %s (%s)" % [target, std.type(target)]
   ),

   shard_count(n):: ([{ name: x } for x in std.range(1, n)]),

   shard_list(shard, shards):: (
      if shard == "true" || std.type(shard) == "boolean" then
         shards
      else if std.type(shard) == "string" then
         if std.type(shards[shard]) == "number" then
            kpm.shard_count(shards[shard])
         else
            shards[shard]
      else if std.type(shard) == "number" then
         kpm.shard_count(shard)
      else
         error "Unknow shard value: %s (%s)" % [shard, std.type(shard)]
   ),


   template_format(resource):: (
      resource { value: kpm.loadObject(resource.template % resource.variables) }
   ),

   template_jinja2(resource):: (
      resource { value: kpm.loadObject(kpm.jinja2(resource.template, resource.variables)) }
   ),

   template_jsonnet(resource):: (
      resource { value: kpmstd.jsonnet(resource.template, { variables: std.toString(resource.variables) }) }
   ),

   extra_resource_values(resource):: (
   if resource.format == "kubernetes" then
      {
         namespace: resource.variables.namespace,
         value: { metadata: { name: resource.name,
                              namespace: resource.variables.namespace } },
      }
      else {}
   ),

   resource_defaults(resource, package):: (
      local default_expander = if std.objectHas(package, 'expander') then
         package.expander
      else
         'none';
      local platform = if std.objectHas(package, 'format') then
         package.format
      else
         'kubernetes';
      {
         format: platform,
         expander: default_expander,
         protected: false,
      }
   ),

   template(resource):: (
      local r = if std.objectHas(resource, "expander") == false ||
                   resource.expander == 'none' || resource.expander == null then
         resource { value: kpm.loadObject(resource.template) }
      else if resource.expander == "fmt" then
         kpm.template_format(resource)
      else if resource.expander == "jinja2" then
         kpm.template_jinja2(resource)
      else if resource.expander == "jsonnet" then
         kpm.template_jsonnet(resource)
      else
         error "Unknown expander: %s.\n Supported expanders are: [fmt, jinja2, jsonnet, none]" % [resource.expander];
      std.mergePatch(r, kpm.extra_resource_values(resource))
   ),

   sharded_resource(resource, shards, variables):: (
      local resource_shards = kpm.shard_list(resource.sharded, shards);
      [
         local extra = {
            shard: shard.name,
            name: "%s-%s" % [resource.name, shard.name],
         };

         local var = if std.objectHas(shard, "variables") == true then
            { variables: variables + extra +
                         shard.variables + { shards: resource_shards } }
         else
            { variables: variables + extra + { shards: resource_shards } };

         local r = resource + extra + var;
         kpm.template(r) for shard in resource_shards]
   ),

   resources(resources, shards, variables, package):: (
       std.flattenArrays([
         local r = kpm.resource_defaults(resource, package) + resource;
         local result = if std.objectHas(resource, "sharded") then
            kpm.sharded_resource(r, shards, variables)
         else
            [kpm.template(r { variables: { name: r.name } + variables })];
         result for resource in resources if resource != null])
   ),


   ###
   package(pack, env):: (
    local default_params = {
       namespace: "default",
       variables: { namespace: default_params.namespace },
       shards: null,
       };

      local params = std.mergePatch(default_params, env);

      local variables = { variables: { namespace: params.namespace } +
                                     if std.objectHas(pack, "variables") then
         kpm.variables(pack, params)
      else
         params.variables };
      local p = pack + variables;

      local shards = if std.objectHas(p, "shards") then
         { shards: kpm.shards(p.shards, params.shards) }
      else
         { shards: null };

      local resources = if std.objectHas(p, "resources") then
         { resources: kpm.resources(p.resources, shards.shards, variables.variables, p.package) }
      else
         { resources: [] };

      local package = {
         package: p.package,
         deploy: kpmstd.compact(p.deploy) } + variables + shards + resources;

      package
   ),
}
